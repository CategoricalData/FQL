schema Path1 = {  //This is the graph-indexing schema
	nodes A1,A0;
	attributes att1:A1->string,att0:A0->string;
	arrows src1:A1->A0, tgt1:A1->A0;
	equations;
}


 instance G1 = {
	nodes A0->{a,b,c,d,e}, A1->{f,g,h,i,j,k,l};
	attributes 
		att0->{(a,a),(b,b),(c,c),(d,d),(e,e)},
		att1->{(f,f),(g,g),(h,h),(i,i),(j,j),(k,k),(l,l)};
	arrows 
		src1->{(f,a),(g,b),(h,a),(i,c),(j,b),(k,d),(l,e)},
		tgt1->{(f,b),(g,c),(h,b),(i,d),(j,d),(k,e),(l,e)};
} : Path1

schema ReflPath1 = {
	nodes A1,A0;
	attributes att1:A1->string,att0:A0->string;
	arrows src1:A1->A0, tgt1:A1->A0, refl0:A0->A1;
	equations A0.refl0.src1=A0,A0.refl0.tgt1=A0;
}

mapping Reflizer1 = {
	nodes A0->A0, A1->A1;
	attributes att0->att0,att1->att1;
	arrows src1->A1.src1, tgt1->A1.tgt1;
} : Path1 -> ReflPath1

instance Refl_G1=SIGMA Reflizer1 G1

schema Path2 = {
	nodes A2,A1,A0;
	attributes att1:A1->string,att0:A0->string;
	arrows src2:A2->A1, tgt2:A2->A1, src1:A1->A0, tgt1:A1->A0;
	equations A2.src2.tgt1=A2.tgt2.src1;
}

mapping UpTo2 = {
	nodes A0->A0,A1->A1;
	attributes att1->att1,att0->att0;
	arrows 
		src1->A1.src1,
		tgt1->A1.tgt1;
} : Path1 -> Path2

instance G2 = pi UpTo2 G1

schema Refl1Path2 = {
	nodes A2,A1,A0;
	attributes att1:A1->string,att0:A0->string;
	arrows 
		src2:A2->A1, 
		tgt2:A2->A1, 
		src1:A1->A0, 
		tgt1:A1->A0, 
		refl0:A0->A1;
	equations 
		A2.src2.tgt1=A2.tgt2.src1,
		A0.refl0.src1=A0,A0.refl0.tgt1=A0;
}
mapping Reflizer12 = {
	nodes A0->A0, A1->A1, A2->A2;
	attributes att0->att0,att1->att1;
	arrows 
		src1->A1.src1, 
		tgt1->A1.tgt1,
		src2->A2.src2, 
		tgt2->A2.tgt2;
} : Path2 -> Refl1Path2

schema Refl2Path2 = {
	nodes A2,A1,A0;
	attributes att1:A1->string,att0:A0->string;
	arrows 
		src2:A2->A1, 
		tgt2:A2->A1, 
		src1:A1->A0, 
		tgt1:A1->A0, 
		refl0:A0->A1,
		refl1:A1->A2;
	equations 
		A2.src2.tgt1=A2.tgt2.src1,
		A0.refl0.src1=A0,A0.refl0.tgt1=A0,
		A1.refl1.src2=A1,A1.refl1.tgt2=A1;
}

mapping Reflizer2 = {
	nodes A0->A0, A1->A1, A2->A2;
	attributes att0->att0,att1->att1;
	arrows 
		src1->A1.src1, 
		tgt1->A1.tgt1,
		src2->A2.src2, 
		tgt2->A2.tgt2;
} : Path2 -> Refl2Path2



mapping ReflUpTo2 = {
	nodes A0->A0,A1->A1;
	attributes att1->att1,att0->att0;
	arrows 
		src1->A1.src1,
		tgt1->A1.tgt1,
		refl0->A0.refl0;
} : ReflPath1 -> Refl2Path2


instance UpTo2_Refl_G1=pi ReflUpTo2 Refl_G1



schema Path3 = {
	nodes 
		A3,
		A2,
		A1,A0;
	attributes att1:A1->string,att0:A0->string;
	arrows 
		src3:A3->A2, tgt3:A3->A2, 
		src2:A2->A1, tgt2:A2->A1, 
		src1:A1->A0, tgt1:A1->A0;
	equations 
		A3.src3.tgt2=A3.tgt3.src2,
		A2.src2.tgt1=A2.tgt2.src1;
}

mapping UpTo3 = {
	nodes A0->A0,A1->A1,A2->A2;
	attributes att1->att1,att0->att0;
	arrows src1->A1.src1,tgt1->A1.tgt1,src2->A2.src2,tgt2->A2.tgt2;
} : Path2 -> Path3

instance G3 = pi UpTo3 G2

instance Loop = {
	nodes A0->{s}, A1->{f};
	attributes att0->{(s,s)},att1->{(f,f)};
	arrows src1->{(f,s)},tgt1->{(f,s)};
} :  Path1

instance J=pi UpTo2 G1

instance K=pi UpTo2 Loop

instance L=pi UpTo3 K
