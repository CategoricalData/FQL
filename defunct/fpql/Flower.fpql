adom : type

Wisnesky Spivak Chlipala Morrisett Malecha Gross Harvard MIT Math CS : adom

S = schema {
	nodes Person, School, Dept;
	edges advisor : Person -> Person, 
	      instituteOf : Person -> School, 
	      deptOf : Person -> Dept,
	      biggestDept : School -> Dept,
	      lastName : Person -> adom,
	      schoolName : School -> adom,
	      deptName : Dept -> adom;
	equations advisor . advisor = advisor;
}

I = instance {
	variables ryan david adam greg gregory jason : Person,
	          harvard mit : School, math cs : Dept;
	equations ryan.lastName = Wisnesky, ryan.advisor = david, ryan.instituteOf = harvard, ryan.deptOf = math,
	          gregory.lastName = Malecha, gregory.advisor = greg,  gregory.instituteOf = harvard, gregory.deptOf = cs,
	          jason.lastName = Gross, jason.advisor = adam, jason.instituteOf = mit, jason.deptOf = math,
	          adam.lastName = Chlipala, adam.instituteOf = mit, adam.deptOf = cs,
	          greg.lastName = Morrisett, greg.instituteOf = harvard, greg.deptOf = cs,
	          david.lastName = Spivak, david.instituteOf = mit, david.deptOf = math,
	          mit.schoolName = MIT, harvard.schoolName = Harvard,
	          math.deptName = Math, cs.deptName = CS,
	          harvard.biggestDept = math, mit.biggestDept = cs;
} : S 

//Find all the people advised by someone whose school's biggest department is math
BiggestDeptIsMath = flower {
	select p.lastName as name, p.instituteOf.schoolName as school;
	from Person as p;
	where p.advisor.instituteOf.biggestDept.deptName = Math;
} I 

AllPeople = FLOWER {
	select p.lastName as name, p.instituteOf.schoolName as school;
	from Person as p;
	where (p.advisor.instituteOf.biggestDept.deptName = Math or
	       p.advisor.instituteOf.biggestDept.deptName = CS);
} I 

//Find all people sharing the same school, and the name of their schools
SameSchool = flower {
	select p1.lastName as name1, p2.lastName as name2, p1.instituteOf.schoolName as school;
	from Person as p1, Person as p2;
	where p1.instituteOf = p2.instituteOf;
} I 

///////////
