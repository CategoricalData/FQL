String : type 
a b GoneWind Catch22 CatTheory : String

Isbn_num : type 
isbn1 isbn2 isbn3 isbn249 isbn258 isbn4597 : Isbn_num

R : type 
zero : R 
succ : R -> R 
eq0 : succ.succ.succ.succ.succ = succ //otherwise, pi doesn't terminate

RL3 : type 
one two : RL3 

i : RL3 -> R 
eq1 : one.i = zero.succ
eq2 : two.i = zero.succ.succ

C = schema {
	nodes 
		Book;
	edges 
		price:Book->R,
		title:Book->String,
		isbn:Book->Isbn_num;	
	equations;
}

//Note the last digit of the ISBN is the number of letters in the title.
I = instance {
	  variables 
	   book1 book2 book3:Book;
	  equations
      book1.title = GoneWind, book2.title=Catch22, book3.title=CatTheory,
      book1.isbn = isbn258, book2.isbn = isbn4597, book3.isbn = isbn249,
      book1.price = zero.succ.succ.succ.succ, book2.price = zero.succ.succ.succ, book3.price = zero.succ;
} : C

//Note that there is exactly one book whose price is less than 3.
D = schema {
	  nodes
	   W,
	   Book;
	  edges 
		title:Book->String,
		isbn:Book->Isbn_num,
		f:W->RL3,
		g:W->Book,
		price:Book->R;	
	equations
		W.f.i=W.g.price;
}

F = mapping {
     nodes 
	   Book->Book;
	  edges 
	   price->Book.price,
	   title->Book.title,
	   isbn->Book.isbn;
} : C -> D


pi_F_I = pi F I


E = schema {
	nodes W;
	edges title:W->String, isbn:W->Isbn_num;
	equations;
}

G = mapping {
	nodes W->W;
	edges title->W.g.title, isbn->W.g.isbn;
} : E -> D

Select = delta G pi_F_I
